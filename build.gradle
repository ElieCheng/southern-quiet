plugins {
    id 'idea'
    id 'eclipse'
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'

    id 'org.jetbrains.kotlin.jvm' version '1.2.51'
    id 'org.springframework.boot' version '2.1.6.RELEASE'
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
}

idea {
    project {
        jdkName = JavaVersion.VERSION_1_8.toString()
        languageLevel = JavaVersion.VERSION_1_8.toString()
    }
}

allprojects {
    group = 'me.insidezhou.southernquiet'
    version = '3.0.1'

    ext {
        instepVersion = '1.0.1'
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    task showVersion {
        doLast {
            println 'Version: ' + project.version
        }
    }
}

subprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    dependencies {
        compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        compileClasspath "org.springframework.boot:spring-boot-configuration-processor"
        testImplementation "org.springframework.boot:spring-boot-starter-test"
    }

    compileJava.dependsOn(processResources)

    tasks.withType(JavaCompile) {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8

        options.encoding = "UTF-8"
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_1_8.toString()
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_1_8.toString()
        }
    }

    jar {
        enabled = true
        archiveBaseName.set("$rootProject.name-$project.name")

        manifest {
            attributes 'Implementation-Version': version
        }
    }

    javadoc {
        options.encoding = 'UTF-8'
        failOnError = false
    }

    bootJar {
        archiveClassifier.set('boot')

        manifest {
            attributes 'Implementation-Version': version
        }
    }

    task sourceJar(type: Jar) {
        archiveClassifier.set('sources')
        from sourceSets.main.allSource
    }

    task docJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier.set('javadoc')
        from javadoc.destinationDir
    }

    publishing {
        publications {
            full(MavenPublication) {
                artifactId jar.baseName

                from components.java

                artifact sourceJar
                artifact docJar

                pom {
                    name = 'SouthernQuiet'
                    description = '这是一个基于Spring Boot，为了复用一些公共代码而存在的库。'
                    url = 'https://github.com/InsideZhou/southern-quiet'

                    scm {
                        connection = 'scm:git:git://github.com/InsideZhou/southern-quiet.git'
                        developerConnection = 'scm:git:ssh://github.com/InsideZhou/southern-quiet.git'
                        url = 'https://github.com/InsideZhou/southern-quiet'

                    }

                    licenses {
                        license {
                            name = 'The 3-Clause BSD License'
                            url = 'https://opensource.org/licenses/BSD-3-Clause'
                        }
                    }

                    developers {
                        developer {
                            name = 'Inside Zhou'
                            email = 'inside@insidezhou.me'
                        }
                    }
                }
            }

            simple(MavenPublication) {
                artifactId jar.baseName

                from components.java

                artifact sourceJar
            }
        }

        repositories {
            maven {
                def isSnapshot = version.endsWith("SNAPSHOT")
                def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'

                url isSnapshot ? snapshotsRepoUrl : releasesRepoUrl
                name 'sonatype'
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        }
    }

    signing {
        sign publishing.publications.full
    }
}
repositories {
    mavenCentral()
}
dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
