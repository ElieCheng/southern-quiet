buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven { url "http://repo.spring.io/milestone" }
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.0.0.M1'
    }
}

plugins {
    id 'idea'
    id 'eclipse'
}

idea {
    project {
        jdkName = '1.8'
        languageLevel = '1.8'
    }
}

subprojects {
    group = 'com.ai.service-framework'
    version = '0.0.1-SNAPSHOT'

    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    configurations {
        provided
    }

    idea {
        module {
            scopes.PROVIDED.plus += [configurations.provided]
        }
    }

    sourceSets {
        main { compileClasspath += configurations.provided }
    }

    repositories {
        mavenLocal()
        jcenter()
        maven { url "http://repo.spring.io/milestone" }
    }

    dependencies {
        provided "org.springframework.boot:spring-boot-configuration-processor"

        testCompile "org.springframework.boot:spring-boot-starter-test"
    }

    compileJava.dependsOn(processResources)

    tasks.withType(JavaCompile) {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8

        options.encoding = "UTF-8"
    }

    task sourceJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives jar, sourceJar
    }
}
